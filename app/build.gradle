apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.neenbedankt.android-apt'

def PACKAGE_NAME = "com.sooch.qiita_reader"

def VERSION_MAJOR = 0
def VERSION_MINOR = 1
def VERSION_PATCH = 0

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.1"

    defaultConfig {
        applicationId PACKAGE_NAME
        minSdkVersion 15
        targetSdkVersion 24
        versionCode VERSION_MAJOR * 100 + VERSION_MINOR * 10 + VERSION_PATCH
        versionName "${VERSION_MAJOR}.${VERSION_MINOR}.${VERSION_PATCH}"
    }

    signingConfigs {
        debug {
            storeFile rootProject.file("debug.keystore")
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
        release {
            storeFile rootProject.file("debug.keystore")
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
    }

    productFlavors {
        production {
        }
    }

    buildTypes {
        debug {
            debuggable true
            applicationIdSuffix ".debug"
            versionNameSuffix '-debug'
        }

        release {
            debuggable false
            minifyEnabled true    // ProGuardを有効にする
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    packagingOptions {
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dataBinding {
        enabled = true
    }

    // Rename .apk (Instant Runを有効にしてください)
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            if (output.outputFile != null && output.outputFile.name.endsWith('.apk')) {
                def file = output.outputFile
                def date = new java.text.SimpleDateFormat("yyyyMMdd-HHmmss").format(new Date())
                def newName = "app-${variant.flavorName}-${variant.buildType.name}_v${defaultConfig.versionName}_${date}.apk"
                output.outputFile = new File(file.parent, newName)
            }
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile "com.android.support:appcompat-v7:${support_lib_version}"
    compile "com.android.support:design:${support_lib_version}"

    // Google Play Service
    // https://developer.android.com/google/play-services
    // https://developers.google.com/android/guides/setup#add_google_play_services_to_your_project
//    compile "com.google.android.gms:play-services:${play_services_lib_version}"

    // Retrofit2, Json Converter, Adapter
    // https://github.com/square/retrofit
    compile "com.squareup.retrofit2:retrofit:${retrofit_lib_version}"
    compile "com.squareup.retrofit2:converter-gson:${retrofit_lib_version}"
    compile "com.squareup.retrofit2:adapter-rxjava:${retrofit_lib_version}"

    // Dagger2
    // https://github.com/google/dagger
    compile "com.google.dagger:dagger:${dagger2_lib_version}"
    apt "com.google.dagger:dagger-compiler:${dagger2_lib_version}"

    // OkHttp3
    // https://github.com/square/okhttp
    compile "com.squareup.okhttp3:okhttp:${okhttp_lib_version}"
    compile "com.squareup.okhttp3:logging-interceptor:${okhttp_lib_version}"

    // RxJava
    // https://github.com/ReactiveX/RxJava
    compile 'io.reactivex:rxjava:1.0.10'

    // RxAndroid
    // https://github.com/ReactiveX/RxAndroid
    compile 'io.reactivex:rxandroid:1.0.1'

    // Picasso
    // http://square.github.io/picasso/
    compile 'com.squareup.picasso:picasso:2.5.2'

    // multiline-collapsingtoolbar
    // https://github.com/opacapp/multiline-collapsingtoolbar
    compile 'net.opacapp:multiline-collapsingtoolbar:1.2.2'

    // Extends Fab
    // https://github.com/Clans/FloatingActionButton
    compile 'com.github.clans:fab:1.6.4'

}
